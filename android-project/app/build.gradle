apply plugin: 'com.android.application'
import org.apache.tools.ant.taskdefs.condition.Os

android {
    compileSdkVersion 22
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.android.jxcore.jxcoresample"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }

    sourceSets.main {
        jniLibs.srcDir 'src/main/libs'
        jni.srcDirs = []
    }

    task ndkBuild(type: Exec) {
        workingDir file('src/main/').getAbsolutePath()
        commandLine getNDKCmd()
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    task cleanNative(type: Exec) {
        workingDir file('src/main')
        commandLine getNDKCmd(), 'clean'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

def findNDK() {
    Properties p = new Properties()
    p.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndir = p.getProperty('ndk.dir', null)
    if (ndir == null) {
        throw new GradleException("You must define ndk.dir under local.properties file.")
    }

    return ndir
}

def getNDKCmd() {
    def cmd = findNDK() + "/ndk-build"
    if (Os.isFamily(Os.FAMILY_WINDOWS))
        cmd += ".cmd"

    return cmd
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
}
